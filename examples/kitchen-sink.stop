/**
 * This Stop file seeks to throw the kitchen sink at the parser and exercise every language feature.
 */

// Single Line Comment

/**
 * Multi-line comment
 */

// This includes the Header and Parameter models
include "./examples/kitchen-sink-include.stop"

Request {
    enum Method {
        GET
        POST
        PUT
        DELETE
        OPTIONS
    }
    Method method
    string path
    string body
    [Header] headers
    [Parameter] parameters
}

start Router {
    Request request
    -> Login
    -> Authenticate
    -> Dashboard
    -> Profile
}

User {
    sint64 id
    string email
    string username
}

UserNotFoundError {
    string  message
    -> Render
}

async GetUserWithPassword <- User throws UserNotFoundError {
    string login
    string password
    timeout 5
}

Post {
    string  title
    string  body
    bool    public
}

async GetUserPosts <- [Post] throws UserNotFoundError {
    sint64 user_id
    timeout 5
}

FilterPublicPosts <- [Post] {
    [Post] posts
}

Login {
    -> Render
}

Authenticate {
    string login
    string password
    User user <- GetUserWithPassword
    -> Redirect
}

Dashboard {
    sint64 user_id
    [Post] posts <- GetUserPosts
    -> Render
}

Profile {
    sint64 user_id
    [Post] posts <- GetUserPosts
    [Post] publicPosts <- FilterPublicPosts
    -> Render
}

Render {
    string html
    -> Response
}

Redirect {
    string url
    -> Response
}

stop Response {
    string content_type
    string content
    sint32 status
    [Header] headers
}